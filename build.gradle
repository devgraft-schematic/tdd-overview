plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
}

group = 'devgraft'
version = null
sourceCompatibility = '17'

jar { enabled = false }

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    cucumberRuntime {
        extendsFrom testImplementation
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

repositories {
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    // mapper
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    // jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    //h2 - local running
    runtimeOnly 'com.h2database:h2'
    testRuntimeOnly 'com.h2database:h2'
    // integrationTest
    testImplementation 'io.cucumber:cucumber-java8:7.11.0'
    testImplementation 'io.cucumber:cucumber-spring:7.11.0'
    testImplementation 'io.cucumber:cucumber-junit:7.11.0'
    testImplementation 'com.github.madvirus:cucumber-tables:0.5.1'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

sourceSets {
    integrationTest {
        java {
            srcDir 'src/integrationTest/java'
        }
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

task cucumber(type: Test) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

test {
    useJUnitPlatform()
}